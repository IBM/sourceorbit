                R REFER
      *
                  ADID           5P 0       TEXT('ADDRESS ID')
                                            COLHDG('ADRES.' 'ID')
                                            EDTCDE(Z)
                  ADRLINE       50          TEXT('ADDRESS LINE')
                                            COLHDG('ADDRESS' 'LINE')
                  ARID           6          TEXT('ARTICLE ID')
                                            COLHDG('ART.' 'ID')
                  ARDESC        50          TEXT('ARTICLE DESCRPTION')
                                            COLHDG('DESCRIPTION')
                  CUID           5P 0       TEXT('CUSTOMER ID')
                                            COLHDG('CUST' 'ID')
                                            EDTCDE(Z)
                  COID           2          TEXT('COUNTRY CODE')
                                            COLHDG('CO' 'ID')
                  COUNTR        30          TEXT('COUNTRY NAME')
                                            COLHDG('COUNTRY' 'NAME')
                  CITY          30          TEXT('CITY')
                                            COLHDG('CITY')
                  CUSTNM        30          TEXT('CUSTOMER NAME')
                                            COLHDG('CUSTOMER' 'NAME')
                  DLCODE         1          COLHDG('DEL' 'CD')
                                            TEXT('DELETE CODE X=DELETED')
                  FAID           3          TEXT('FAMILLY ID')
                                            COLHDG('FAM' 'ID')
                  FADESC        50          TEXT('FAMILLY DESCRIPTION')
                                            COLHDG('FAMILLY')
                  EMAIL         50          TEXT('ADDRESS MAIL')
                                            COLHDG('ADDRESS' 'MAIL')
                  ORID           6P 0       TEXT('ORDER NUMBER')
                                            COLHDG('ORD' 'NUM')
                                            EDTCDE(Z)
                  ODLINE         5P 0       TEXT('ORDER LINE')
                                            COLHDG('ORD' 'LINE')
                                            EDTCDE(Z)
                  PRID           5P 0       TEXT('PROVIDER ID')
                                            COLHDG('PROVIDER' 'ID')
                                            EDTCDE(Z)
                  PROVNM        30          TEXT('PROVIDER NAME')
                                            COLHDG('PROVIDER' 'NAME')
                  PHONE         15          TEXT('PHONE NUMBER')
                                            COLHDG('PHONE' 'NUMBER')
                  QUANTITY       5  0       TEXT('QUANTITY')
                                            EDTCDE(Z)
                                            COLHDG('QTY')
                  TOTPRICE       9P 2       TEXT('TOTAL PRICE')
                                            COLHDG('TOTAL' 'PRICE')
                                            EDTCDE(2)
                  UNITPRICE      7P 2       TEXT('UNIT PRICE')
                                            COLHDG('UNIT' 'PRICE')
                                            EDTCDE(2)
                  WHID           3          TEXT('WAREHOUSE ID')
                                            COLHDG('WHS' 'ID')
                  VATCODE        1          TEXT('VAT CODE')
                                            COLHDG('VAT' 'CODE')
                                            DFT('2')
                  VATRATE        4  2       TEXT('VAT RATE %')
                                            COLHDG('VAT' 'RATE %')
                                            EDTWRD('  ,  %')
                  VATNUM        12          TEXT('VAT NUMBER')
                                            COLHDG('VAT' 'NUMBER')
                  WHNAME        20          TEXT('WAREHOUSE NAME')
                                            COLHDG('WAREHOUSE')
                  YEAR           4P 0       TEXT('YEAR')
                                            COLHDG('YEAR')
                  ZIPCOD        10          TEXT('ZIP CODE')
                                            COLHDG('ZIP' 'CODE')